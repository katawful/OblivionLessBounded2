Scriptname CharGenQuest

short init
short convCount
short warnCount
short hurryCount
short birthsignCount
short birthsignChoice
short tauntCount
short tauntStage
short what
short who
short why
short die
short fate
short tookArmor
short inAmbushBRoom	; set to 1 by trigger zone when player jumps down into Ambush B room

short baurusWarn		; Baurus has warned player not to follow
float baurusTimer		; used to time Baurus's warnings before Emp is killed

short ambushCount	; how many assassins dead?

short playerKill			; how many assassins has the player killed?

short secretDoor		; set to 1 when Renote triggers secret door

short finalTauntCount 	; used to conditionalize final assassin's speech

; used by conversation scripts to tell whose turn to speak
short speaker
short target
; 1 = Baurus
; 2 = Renote
; 3 = Glenroy
; 4 = Emperor
; 5 = player (as target)

; used to time space between conversation items
float convTimer

; used by idle manager to determine which unique idle to play
short uniqueIdle
; 1 = Halt!
; 2 = Cross arms
; 3 = Point Forward
; 4 = Point Left
; 5 = Point Right
; 6 = Point Up
; 7 = Check Corpse
; 8 = One hand check weapon hilt
; 9 = Look at weapon
; 10 = Follow me!
; 11 = Stay here (point down)
; 12 = Point Up WEAPON
; 13 = Point Forward WEAPON
; 20 = Talk arms crossed
; 21 = Talk hands at side
; 22 = Talk both hands
; 23 = Talk hands at side 2
; 24 = Fist clinched
; 25 = Scan
; 26 = Alerted Scan
; 27 = Hold Up!
; 28 = Exhausted
; 29 = Death scene
; 30 = Death scene loop

; this will be 1 through the duration of chargen quest
short charGenRunning

; set to 1 to see debug messages in chargen
short debug

float fQuestDelayTime	; for quest script timing

;;;KAT INFO: We want to close the race menu asap, and set the weather and time ideally
begin menumode 1036
	if GetStage Charactergen < 7
		;;KAT NOTE: I don't know if I want clear weather or not
		;ForceWeather Clear
		Set GameHour to 7
		CloseAllMenus
	endif
end


begin gamemode

	; count down timer
	if eval convTimer > 0
		set convTimer to convTimer - GetSecondsPassed
	endif

	; bring up race menu first
	;;KAT INFO: "Charactergen" quest starts at 0 and jumps to 5 to set the player's location
	if eval GetStage Charactergen == 0
		SetStage Charactergen 5
	endif

	;;KAT INFO: Handle controls and first person in gamemode
	if eval GetStage Charactergen < 7 && GetStage Charactergen != 0
		ToggleFirstPerson 1 ; force 1st person no matter what

		;; DISABLE MOUSE
		call OLB2ControlMouse 0

		;; DISABLE CONTROLS
		Call OLB2ControlControls 0
	endif

	; *********************************************************************************************
	; Large IF block to help script processing speed (skip over chunks when not needed)
	; *********************************************************************************************
	IF eval GetStage Charactergen < 30

		; when emperor finished reacting to player, trigger him to force-greet
		if eval GetStage Charactergen == 16 && convTimer <= 0
			SetStage Charactergen 17
		endif


		; when secret door is open, trigger everyone to go through it
		if eval GetStage Charactergen == 18 && secretDoor == 1 && convTimer <= 0
			SetStage Charactergen 19
		endif

		; trigger ambush A
		if eval GetStage Charactergen == 20 && ( CGMarkerCEmperor.GetDistance Player < 480 || CGMarkerC2Renote.GetDistance Player < 900 )
			SetStage Charactergen 22
		endif

		; make sure things progress even if player isn't listening to conversation
		if eval GetStage Charactergen == 26 && Player.GetDistance GlenroyRef > 2000
			if eval debug == 1
				MessageBox "CharGenQuest script: setting stage 28 because player is not near Glenroy"
			endif
			SetStage Charactergen 28
		endif

		; Baurus warns player not to follow
		if eval GetStage Charactergen == 28 && Player.GetDistance BaurusRef < 800 && convTimer <= 0 && baurusWarn == 0
			set convTimer to BaurusRef.SayTo Player CharGenVoice 1
			set baurusWarn to 1
		endif

		if eval GetStage Charactergen == 28 && convTimer <= 0 && baurusWarn == 1
			SetStage Charactergen 29
		endif

	ENDIF	; stage < 30
	; *********************************************************************************************



	; *********************************************************************************************
	; Large IF block to help script processing speed (skip over chunks when not needed)
	; *********************************************************************************************
	IF eval GetStage Charactergen >= 30 && GetStage Charactergen < 50

		; next phase -- assassins are dead
		if eval GetStage Charactergen == 34 && ambushCount >=2
			SetStage Charactergen 36
		endif


		; next phase -- player gets close to NPCs in Room B
		if eval GetStage Charactergen >= 36 && GetStage Charactergen < 40 && inAmbushBRoom == 1
			if eval ( GlenroyRef.GetDetected Player == 1 || BaurusRef.GetDetected Player == 1 )
				set ambushCount to 2
				SetStage Charactergen 40
			endif
		endif

		; put up birthsign menu
		if eval GetStage Charactergen == 43
			ShowBirthSignMenu
			SetStage Charactergen 44
		endif

		if eval GetStage Charactergen == 45 && convTimer <= 0
			SetStage Charactergen 50
		endif

	ENDIF	; getstage charactergen >= 30 && getstage charactergen < 50
	; *********************************************************************************************



	; *********************************************************************************************
	; Large IF block to help script processing speed (skip over chunks when not needed)
	; *********************************************************************************************
	IF eval GetStage Charactergen >= 50 && GetStage Charactergen < 70


		; next phase -- player gets close to NPCs at Marker F
		if eval GetStage Charactergen == 52 && Player.GetDistance UrielSeptimRef < 500
			; move out
			SetStage Charactergen 54
		endif

		; next phase -- player gets close to NPCs at Marker G
		if eval GetStage Charactergen == 56 && Player.GetDistance UrielSeptimRef < 500
			; wait until Baurus and Emperor are both in the cell before proceeding
			if eval BaurusRef.GetInCell ImperialDungeon03 == 1 && UrielSeptimRef.GetInCell ImperialDungeon03
				; move out
				SetStage Charactergen 58
			endif
		endif

		if eval GetStage Charactergen == 58 && convTimer <= 0
			SetStage Charactergen 59
		endif

		; after Glenroy & Baurus discuss locked gate at I, move on
		if eval GetStage Charactergen == 65 && convTimer <= 0
			SetStage Charactergen 66
		endif

		; next phase -- player gets close to NPCs at Marker J
		if eval GetStage Charactergen == 68 && Player.GetDistance UrielSeptimRef < 300
			; move out
			SetStage Charactergen 70
		endif

	ENDIF	; getstage charactergen >= 50 && getstage charactergen < 70
	; *********************************************************************************************



	; *********************************************************************************************
	; Large IF block to help script processing speed (skip over chunks when not needed)
	; *********************************************************************************************
	IF eval GetStage Charactergen >= 70


		; after Glenroy & Baurus realize they are trapped, move on
		if eval GetStage Charactergen == 71 && convTimer <= 0
			SetStage Charactergen 72
		endif

		; after pause, Emperor will force-greet player
		if eval GetStage Charactergen == 72 && convTimer <= 0
			SetStage Charactergen 73
		endif

		; generate new assassins for Ambush C
		if eval GetStage Charactergen >= 72 && GetStage Charactergen < 74
			if eval GetDeadCount CGMythicDawnAssassinAmbushC >= ambushCount
				CGAmbushCMarker.PlaceAtMe CGMythicDawnAssassinAmbushC 1
				CGAmbushC2Marker.PlaceAtMe CGMythicDawnAssassinAmbushC 1
				CGAmbushC3Marker.PlaceAtMe CGMythicDawnAssassinAmbushC 1
				set ambushCount to GetDeadCount CGMythicDawnAssassinAmbushC + 3
				if eval debug == 1
					MessageBox "DEBUG: CharGen generating 3 new assassins."
				endif
				if eval ambushCount >= 6 && GlenroyRef.GetDead == 0
					set GlenroyRef.dieNextHit to 1
				endif
			endif
		endif

		; after assassin speaks to player, move on
		;if getstage CharacterGen == 80 && convTimer <= 0
		;	setstage CharacterGen 82
		;endif

		; after Baurus kneels by Emperor, move on
		if eval GetStage Charactergen == 85 && convTimer <= 0
			SetStage Charactergen 86
		endif


		if eval baurusTimer > 0
			set baurusTimer to baurusTimer - GetSecondsPassed
		else
			if eval GetStage Charactergen == 73 && Player.GetDistance UrielSeptimRef > 500 && Player.GetDistance BaurusRef < 800
				BaurusRef.SayTo Player CharGenVoice
				set baurusTimer to 15
			endif
		endif

	ENDIF	; getstage charactergen >= 70
	; *********************************************************************************************

end